plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.ben-manes.versions' version '0.52.0'
}

group = 'com.pagseguro'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://packages.confluent.io/maven'
	}
}

configurations {
	avroTools
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.kafka:spring-kafka'

	// avro
	implementation 'io.confluent:kafka-avro-serializer:7.9.1'
	implementation 'org.apache.avro:avro:1.12.0'
	avroTools 'org.apache.avro:avro-tools:1.12.0'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('generateAvroJava', JavaExec) {
	group = "build"
	description = "Compila arquivos .avsc para Java"
	classpath = configurations.avroTools
	mainClass = 'org.apache.avro.tool.Main'
	args = ['compile', 'schema', 'src/main/resources/avro', 'src/main/java']

	outputs.dir file('src/main/java')
}

compileJava.dependsOn generateAvroJava

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
	}
}
